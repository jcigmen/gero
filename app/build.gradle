apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply from: '../config/quality/jacoco.gradle'
apply from: '../config/quality/quality.gradle'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools
    dataBinding {
        enabled true
    }
    defaultConfig {
        applicationId "org.noobs2d.gero"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.name
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        warningsAsErrors true
        abortOnError true // Fail early.

        disable 'GoogleAppIndexingWarning' // App does not have deep linking.
        disable 'InvalidPackage' // Okio references java.nio that does not presented in Android SDK.
    }
    sourceSets {
        test.java.srcDir 'src/unitTests/java'
        test.java.srcDir 'src/integrationTests/java'
        androidTest.java.srcDir 'src/functionalTests/java'
    }
}

dependencies {
    annotationProcessor annotationProcessors.autoValue
    annotationProcessor annotationProcessors.dagger

    androidTestCompile testLibraries.espresso
    androidTestCompile testLibraries.espressoContrib
    androidTestCompile testLibraries.mockito
    androidTestCompile testLibraries.supportTestRules
    androidTestCompile testLibraries.supportTestRunner

    compile libraries.dagger
    compile libraries.gson
    compile libraries.mosbyMvp
    compile libraries.picasso
    compile libraries.retrofit
    compile libraries.retrofitGsonAdapter
    compile libraries.rxJava
    compile libraries.rxPreferences
    compile libraries.supportAnnotations
    compile libraries.supportAppCompat
    compile libraries.supportCardView
    compile libraries.supportDesign
    compile libraries.supportRecyclerView

    debugCompile debugTools.leakCanary

    provided libraries.autoValue

    releaseCompile debugTools.leakCanaryRelease

    testCompile debugTools.leakCanaryTest
    testCompile testLibraries.junit
    testCompile testLibraries.mockito
}
